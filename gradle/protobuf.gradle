
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

dependencies {

    compile("com.google.code.gson:gson:2.8.1")
    compile("com.google.protobuf:protobuf-java:${project.'protobuf.version'}")
    compile("com.google.protobuf:protobuf-java-util:${project.'protobuf.version'}")

    if(JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compile('javax.annotation:javax.annotation-api:1.3.1')
    }

}

clean {
    delete protobuf.generatedFilesBaseDir
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${project.'protobuf.version'}"
    }
}

sourceSets {
    main {
        java {
            srcDirs '/build/generated/source/proto/main/java'
        }
    }
}

idea {
    module {
        // Not using generatedSourceDirs because of 
        // https://discuss.gradle.org/t/support-for-intellij-2016/15294/8
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java")
    }
}